// from "ecc/proofOfOwnership" import main as proofOfOwnership;
// from "ecc/babyjubjubParams" import main as getContext, BabyJubJubParams;

struct Transaction {
    u32 sourceIndex;
    u32 targetIndex;	
	u32 amount;
    u32 nonce;
}

struct TransactionHelper {
	u32[8] sourceAddress; // in tezos this are public keys instead of addresses
    u32[8] targetAddress; // in tezos this are public keys instead of addresses
    u64[8] signature;     // signed operation
}

def checkTransactions<N, M>(TransactionHelper[N] transactionExtras, u32[M][8] hashedAccounts, Transaction[N] transactions) -> bool {
    // BabyJubJubParams context = getContext();
    
    for u32 i in 0..N {
        TransactionHelper transactionHelper = transactionExtras[i];
        Transaction transaction = transactions[i];


        // check that sender and recipient address are in the list
        assert(hashedAccounts[transaction.sourceIndex] == transactionHelper.sourceAddress);
        assert(hashedAccounts[transaction.targetIndex] == transactionHelper.targetAddress);

        // check correctness of signature
        // bool correctTransaction = proofOfOwnership(transaction.sourceAddress, transaction.signature, context);
        // assert(correctTransaction);
    }

    return true;
}

def calculateNewBalancesAndNonces<N, M>(u32[M] balances, u32[M] nonces, Transaction[N] transactions, TransactionHelper[N] transactionExtras) -> u32[2][M] {

    u32[M] mut newBalances = balances;
    u32[M] mut newNonces = nonces;

    for u32 i in 0..N {
        Transaction transaction = transactions[i];
        TransactionHelper transactionHelper = transactionExtras[i];

        // check that the sender balance is sufficient
        assert(newBalances[transaction.sourceIndex] >= transaction.amount);

        // update balances
        newBalances[transaction.sourceIndex] = newBalances[transaction.sourceIndex] - transaction.amount;
        newBalances[transaction.targetIndex] = newBalances[transaction.targetIndex] + transaction.amount;

        // check that nonce is the expected one
        assert(transaction.nonce == (newNonces[transaction.sourceIndex]) + 1);

        // increment the nonce array
        newNonces[transaction.sourceIndex] = transaction.nonce;
    }

    return [newBalances, newNonces];
}