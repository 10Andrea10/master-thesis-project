from "./../../lib/merkleTree" import generateMerkleTreePoseidon;
from "./../../lib/transactions" import calculateNewBalancesAndNonces, checkTransactions;
from "./../../lib/hash" import hashCouplesArrays;
from "./../../lib/types" import Transaction, TransactionHelper;


const u32 TREE_DEPTH = 2;
const u32 TRANSAC_NUM = 30;
const u32 ACCOUNT_NUM = 4;

def main(u32[8] accountRoot, private u32[ACCOUNT_NUM][8]hashedAccounts, u32[8] balancesNoncesRoot, private u32[ACCOUNT_NUM] balances, private u32[ACCOUNT_NUM] nonces, Transaction[TRANSAC_NUM] transactions, private TransactionHelper[TRANSAC_NUM] transactionExtras) -> (u32[8], u32[ACCOUNT_NUM], u32[ACCOUNT_NUM]) {
    
    // verify that the accounts are the right ones
    u32[8] generatedAccountRoot = generateMerkleTreePoseidon(hashedAccounts, TREE_DEPTH);
    log("account root generated {}", generatedAccountRoot);
    assert(accountRoot == generatedAccountRoot);

    // verify that the balances + nonces inputed are the right ones
    u32[ACCOUNT_NUM][8] balancesNonces = hashCouplesArrays(balances, nonces);
    u32[8] generatedBalancesNoncesRoot = generateMerkleTreePoseidon(balancesNonces, TREE_DEPTH);
    log("balance + nonces root generated {}", generatedBalancesNoncesRoot);
    assert(balancesNoncesRoot == generatedBalancesNoncesRoot);

    // iterate through each transaction and check that the corresponding signature and public key are correct
    bool correctTransactions = checkTransactions(transactionExtras, hashedAccounts, transactions);
    log("transactions verified: {}", correctTransactions);
    assert(correctTransactions);

    // iterate through each transaction and update the balances and nonces of each account
    u32[2][ACCOUNT_NUM] newBalancesAndNonces = calculateNewBalancesAndNonces(balances, nonces, transactions, transactionExtras);

    u32[ACCOUNT_NUM][8] newBalancesNonces = hashCouplesArrays(newBalancesAndNonces[0], newBalancesAndNonces[1]);
    u32[8] newBalancesNoncesRoot = generateMerkleTreePoseidon(newBalancesNonces, TREE_DEPTH);
    log("new balance + nonces root generated {}", newBalancesNoncesRoot);
    
    return (newBalancesNoncesRoot, newBalancesAndNonces[0], newBalancesAndNonces[1]);
}

