from "./../../lib/merkleTree" import merkleTreeProof, generateMerkleTree;
from "./../../lib/concatenator" import concatenateTwoArraysIn256;

const u32 TREE_DEPTH = 2;
const u32 ACCOUNT_NUM = 4;

// This represents the public key of the account edpkuY4Le5Ps78zDSaHqJDuEa7HCbNEu6x5aD3fwiEHL3LR87bGer4
// Used because it is not possible to have in the smart contract's big map an empty element with key type
const u32[8] EMPTY_ACCOUNT = [
    0,
    0,
    0,
    0,
    0,
    0,
    0,
    0
];

def main(u32[8] accountRoot, private u32[ACCOUNT_NUM][8] hashedAccounts, u32[8] balanceNonceRoot, private u32[ACCOUNT_NUM] balances, private u32[ACCOUNT_NUM] nonces, u32 position, private u64[8]signature) -> (u32[8], u32[8]) {

    // verify that the accounts are the right ones
    u32[8] generatedOldAccountRoot = generateMerkleTree(hashedAccounts, TREE_DEPTH);
    assert(accountRoot == generatedOldAccountRoot);
    log("account root generated {}", generatedOldAccountRoot);

    // verify that the balances + nonces inputed are the right ones
    u32[ACCOUNT_NUM][8] balancesNonces = concatenateTwoArraysIn256(balances, nonces);
    u32[8] generatedBalanceNonceRoot = generateMerkleTree(balancesNonces, TREE_DEPTH);
    log("balance + nonces root generated {}", generatedBalanceNonceRoot);
    assert(balanceNonceRoot == generatedBalanceNonceRoot);

    // TODO: signature check: check that the position has been signed with the private key of the pubkey
    // at hashedAccounts[position]

    // verify that the account is drained from the balance
    assert(balances[position] == 0);

    // update the account keys
    u32[ACCOUNT_NUM][8] mut newHashedAccounts = hashedAccounts;
    newHashedAccounts[position] = EMPTY_ACCOUNT;

    // update balances and nonces
    u32[ACCOUNT_NUM] mut newBalances = balances;
    newBalances[position] = 0;
    u32[ACCOUNT_NUM] mut newNonces = nonces;
    newNonces[position] = 0;

    u32[ACCOUNT_NUM][8] newBalancesNonces = concatenateTwoArraysIn256(newBalances, newNonces);
    u32[8] newBalancesNoncesRoot = generateMerkleTree(newBalancesNonces, TREE_DEPTH);
    log("new balance + nonces root generated {}", newBalancesNoncesRoot);

    u32[8] newAccountRoot = generateMerkleTree(newHashedAccounts, TREE_DEPTH);
    log("new account root {}", newAccountRoot);

    return (newAccountRoot, newBalancesNoncesRoot);
}